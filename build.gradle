allprojects {
    group = 'com.github.oreissig.orsig'

    repositories {
        mavenCentral()
    }

    ext.libs = [:]
    libs.osgi = 'org.osgi:org.osgi.core:6.0.0'
    libs.groovy = 'org.codehaus.groovy:groovy-all:2.4.5:indy'
    libs.spock = dependencies.create('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all' // use indy version instead
    }
    libs.mocking = ['cglib:cglib:3.2.0', 'org.objenesis:objenesis:2.2']

    // use invokedynamic for groovy because it's so cool
    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }

    // configure code coverage
    apply plugin: 'jacoco'
    jacoco.toolVersion = '0.7.5.+'
    tasks.withType(Test) { it.finalizedBy 'jacocoTestReport' }
}
